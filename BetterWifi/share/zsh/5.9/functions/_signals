#autoload

# Options (only in separate words):
#
# -a  use all signals (even the pseudo-signals)
# -p  needs a `-' prefix
# -s  SIG prefix allowed
#
# A `-' or `--' as the first argument is ignored.

local expl minus pre sigs
local first last # keep these as strings for -z/-n tests

zparseopts -D -K -E 'p=minus' 'a=last' 's=pre'
if [[ -z "$last" ]]; then
  first=2
  last=-3
else
  first=1
  last=-1
fi
[[ -n "$minus" ]] && minus='-'

[[ "$1" = -(|-) ]] && shift

if [[ -z "$minus" ]] ||
   ! zstyle -T ":completion:${curcontext}:signals" prefix-needed ||
   [[ -prefix -* ]]; then
  local disp tmp

  if zstyle -t ":completion:${curcontext}:signals" prefix-hidden; then
    tmp=( "${(@)signals[first,last]}" )
    disp=(-d tmp)
  else
    disp=()
  fi

  if [[ -n "$pre" && $PREFIX = ${minus}S* ]]; then
    sigs=( "${minus}SIG${(@)^${(@)signals[first,last]:#<->}}" )
    (( $#disp )) && tmp=( "$tmp[@]" "${(@)signals[first,last]}" )
  else
    sigs=()
  fi

  _wanted signals expl signal \
      compadd "$@" "$disp[@]" -M 'm:{a-z}={A-Z}' - \
              "${minus}${(@)^signals[first,last]}" "$sigs[@]"
fi
