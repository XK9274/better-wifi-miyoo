#compdef mat2

# Completion for mat2 0.11.0 (https://0xacab.org/jvoisin/mat2).

local curcontext="$curcontext" state line
typeset -A opt_args
_arguments \
  '(- *)'{-h,--help}'[show help message and exit]' \
  '(-V --verbose)'{-V,--verbose}'[show more verbose status information]' \
  --unknown-members'[policy how to handle unknown members of archive-style files]:policy:(abort omit keep)' \
  --inplace'[clean in place, without backup]' \
  --no-sandbox"[disable bubblewrap's sandboxing]" \
  '(- *)'{-v,--version}"[show program's version number and exit]" \
  '(- *)'{-l,--list}'[list all supported fileformats]' \
  '(- *)'--check-dependencies'[check if mat2 has all the dependencies it needs]' \
  '(-L --lightweight -s --show)'{-L,--lightweight}'[remove SOME metadata]' \
  '(-s --show -L --lightweight)'{-s,--show}'[list harmful metadata detectable by mat2 without removing them]' \
  '*:file:_files -g "*.(epub|pdf|odc|odf|odg|odi|odp|ods|odt|pptx|xlsx|docx|torrent|ncx|tar|xhtml|xhtm|xht|zip|mp3|mpga|mp1|mp2|oga|opus|ogg|spx|flac|wav|gif|jpg|jpe|jfif|jpeg|png|svgz|svg|tiff|tif|bmp|ppm|css|htm|html|cxx|text|log|txt|hh|hxx|el|conf|cc|pm|f90|mpg4|m4v|mp4|wmv|avi)"'

